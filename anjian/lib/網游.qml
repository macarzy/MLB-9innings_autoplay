[Comment]
命令庫是按鍵精靈8.0版推出的全新功能
您可以把自己常用的函數和子程序寫在命令庫裡讓很多個腳本去調用
命令庫最大的優勢是讓多個腳本共享一個命令，修改一處就等於修改多處
目前命令庫功能還在測試當中，有任何建議可以在按鍵精靈論壇提出，網址：http://bbs.ajjl.cn

******注意！這是官方提供的命令庫，請勿修改！避免以後按鍵精靈升級時覆蓋您的修改。******//
******          如需新增命令庫，可在命令庫點擊右鍵選擇「新建」命令庫            ******//



[General]
MacroID=eb6e2723-6175-4804-a00c-d12ea20def68




[Script]
Sub 前台喊話(數組內容,每句間隔秒,循環時間秒)
    //文字="既然你誠心誠意的發問了|那我就大發慈悲的告訴你|為了防止世界被破壞|為了守護世界的和平|貫徹愛與真實的邪惡|可愛又迷人的反派角色|武藏|小次郎|我們是穿梭在銀河中的火箭隊.白洞..喵"
    //數組內容=Split(文字,"|")
    //例子：Call lib.網游.前台喊話(數組內容,1,5)
    Dim 數量,i
    Rem 開始
    For 數量=0 To UBound(數組內容)
        SayString 數組內容(數量)
        KeyPressS 13,1
        For i=1 To 每句間隔秒
            Delay 1000
        Next
    Next 
    For i=1 To 循環時間秒
        Delay 1000
    Next
    Goto 開始
End Sub
Sub 方形漸開線(中心點x坐標,中心點y坐標,兩點間距離,圈數)
    //例子：Call lib.網游.方形漸開線(400,300,20,20)
    Dim x,y,v,i,j,k
    x=中心點x坐標: y=中心點y坐標
    //設置2點間距離
    v=兩點間距離
    i=1
    For 圈數
        j=0:k=v
        For 2
            For i
                x=x+j:y=y+k
                MoveTo x,y
                Delay 10
                //LeftClick 1
            Next 
            j=v:k=0
        Next 
        i=i+1:v=v*(-1)
    Next
End Sub
Sub 圓形漸開線(中心點x坐標,中心點y坐標,兩點間距離,遞增半徑,圈數)
    //例子：Call lib.網游.圓形漸開線(400,300,20,20,20)
    Dim x0,y0,rr,l,n,r,x,y
    //設置圓心坐標
    x0=中心點x坐標:y0=中心點y坐標
    //設置遞增半徑
    rr=遞增半徑
    //設置點間距
    l=兩點間距離
    //初始化角度
    n=0
    //設置第一圈半徑
    r=30
    //設置畫圓圈數
    For 圈數
        While n<3.1415926*2
            //畫圓公式
            x=x0+r*cos(n)
            y=y0-r*sin(n)
            MoveTo x,y 
            Delay 10
            //LeftClick 1
            //l/r：點距除以半徑，取得2點間相對圓心的角度
            //能有效控制掃瞄點的密度，2點間的距離就比較平均
            n=n+l/r
        Wend 
        //畫完一圈後重置角度
        n=0
        //畫完一圈後半徑遞增rr
        r=r+rr
    Next 
End Sub
Sub 橢圓漸開線(中心點x坐標,中心點y坐標,兩點間距離,橫坐標範圍)
    //例子：Call lib.網游.橢圓漸開線(400,300,20,300)
    //聲明變量
    Dim x0,y0,n,x,y,color0,cor,l,r
    //標記漸開線找怪開始
    Rem start
    //變量初始化
    x0=中心點x坐標:n=1:y0=中心點y坐標:x=中心點x坐標
    //設置2點間距離
    l=兩點間距離
    //限定橫坐標範圍不超過800
    While x<中心點x坐標+橫坐標範圍
        //根據漸開線公式計算下一個點的橫坐標並賦值給x
        x=x0+4*(cos(n)+n*sin(n))
        //根據漸開線公式計算下一個點的縱坐標並賦值給y
        y=y0+3*(sin(n)-n*cos(n))
        //在鼠標移動之前，獲取目標點色值並賦值給color0
        color0=GetPixelColor(x,y)
        //馬上移動鼠標到目標點
        Call SetCursorPos(x,y)
        //循環延時
        Delay 10
        //獲取鼠標移動後該點色值並賦值給color
        cor=GetPixelColor(x,y)
        //同一點取得的兩次色值比較，不同，則執行以下的打怪腳本
        If cor<>color0 Then
            //左鍵單擊
            //LeftClick 1
            //打怪延時
            //Delay 3000
        End If 
        //計算當前點(x,y)到原點(x0,y0)的距離
        r=Sqr((x-x0)^2+(y-y0)^2)
        //漸開線參數增量，其中l/r：點距除以半徑，取得2點間相對圓心的角度
        //能有效控制掃瞄點的密度，2點間的距離就比較平均
        n=n+l/r
    Wend 
    Goto start
    //跳轉到開始，重複漸開線搜索
End Sub


//製作：一隻魚
//日期：2009.12.22



